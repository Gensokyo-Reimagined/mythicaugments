plugins {
    id 'java'
    id 'com.gradleup.shadow' version '8.3.5'
    id 'maven-publish'
    id 'xyz.jpenilla.run-paper' version '2.3.1'
}

group = 'net.gensokyoreimagined'
version = '0.0.1-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = "papermc"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        url = "https://mvn.lumine.io/repository/maven-public/"
    }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.21.10-R0.1-SNAPSHOT"
    implementation "org.spongepowered:configurate-hocon:4.1.2"

    compileOnly "org.jetbrains:annotations:24.1.0"
    compileOnly "io.lumine:Mythic-Dist:5.10.1-SNAPSHOT"
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(21)
}

tasks.compileJava {
    options.release = 21
    options.encoding = 'UTF-8'
}

tasks.javadoc {
    options.encoding = 'UTF-8'
}

tasks.shadowJar {
    relocate 'org.spongepowered.configurate', 'net.gensokyoreimagined.libs.configurate'
    minimize()

    archiveClassifier.set('')
}

// Ensure 'assemble' runs 'shadowJar'
tasks.assemble {
    dependsOn(tasks.shadowJar)
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = "private"
            artifactId = rootProject.name
            artifact tasks.shadowJar
        }
    }

    repositories {
        maven {
            name = "gensokyoReimagined"
            url = uri("https://repo.gensokyoreimagined.net")
            credentials {
                username = project.findProperty("gensokyoUser")?.toString() ?: System.getenv("GENSOKYOUSER")
                password = project.findProperty("gensokyoToken")?.toString() ?: System.getenv("GENSOKYOTOKEN")
            }
        }
    }
}